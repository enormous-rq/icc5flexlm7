#!/bin/csh -f
#******************************************************************************
#
#	    COPYRIGHT (c) 1992-1998 by Globetrotter Software Inc.
#	This software has been provided pursuant to a License Agreement
#	containing restrictions on its use.  This software contains
#	valuable trade secrets and proprietary information of 
#	Globetrotter Software Inc and is protected by law.  It may 
#	not be copied or distributed in any form or medium, disclosed 
#	to third parties, reverse engineered or used in any manner not 
#	provided for in said License Agreement except with the prior 
#	written authorization from Globetrotter Software Inc.
#
#******************************************************************************
#
#	DEMO_INSTALL - Install and run the FLEXlm demo
#
#	M. Christiano
#	8/15/92
#
#	Last changed:  8/25/98
#
echo "Flexible License Manager DEMO install/test"
echo "Copyright (C) 1992-1994 Globetrotter Software, Inc."
echo "All rights reserved."
echo ""

if (-e machind) then
	set KITDIRS = (alpha* apollo* convex* c90* decs* \
		dg* hal* hp* i86* mips* moto* mx300* \
		ncr* nec* next* rm400* ppc* rs6000* \
			sgi* sony* sun* ymp* vax*)
else
	if (! -e lm_code.h) then
		echo "DEMO_INSTALL must be from the FLEXlm install directory"
		echo "	exiting..."
		exit
	endif
	set KITDIRS = (../alpha* ../apollo* ../convex* ../c90* ../decs* \
		../dg* ../hal* ../hp* ../i86* ../mips* ../moto* ../mx300* \
		../ncr* ../nec* ../next* ../rm400* ../ppc* ../rs6000* \
			../sgi* ../sony* ../sun* ../ymp* ../vax*)
endif

set here = `pwd`

set onsco = FALSE
if (-e /dos || -e /usr/odt) then
	set onsco = TRUE
endif


again:
set num = 0
foreach i ($KITDIRS)
	if ("$i" != "../scoinfo.o" && -e $i) then
		if (-e $i/liblmgr.a) then
			set kit = $i
@ 			num++
		endif
	endif
end
echo ""

if ($num == "1") then
	echo "Your demo kit is for the $kit platform"
	echo ""
else
	set num = 0
	echo "Your FLEXlm kits are in:"
	foreach i ($KITDIRS)
		if ("$i" != "../scoinfo.o" && -e $i) then
			if (-e $i/liblmgr.a) then
				echo $i
				set kit = $i
@ 				num++
			endif
		endif
	end
	echo ""
	echo -n "Which kit would you like to install? [pick one from the preceeding list] "
	if ($onsco == "TRUE") then
		set kit = `head -1 < /dev/tty`
	else
		set kit = ($<)
	endif
endif
if (-e $kit/liblmgr.a) then
	echo "Installing FLEXlm kit $kit"
else
	echo "$kit is not a FLEXlm kit directory, please try again"
	echo ""
	goto again
endif

pushd $kit >& /dev/null

set doranlib = FALSE
foreach i ($path)
	if (-e $i/ranlib) then
		set doranlib = TRUE
	endif
end
if ($doranlib == "TRUE") then
	echo ranlib-ing libraries -- please ignore any \"no symbol table\" warning messages
	ranlib *.a
else
	echo "No ranlib found in path, assuming ranlib not needed on this platform"
endif

echo "Creating ANY license file for test purposes in $kit"
rm -f license.dat
echo "SERVER `hostname` ANY" > license.dat
echo "VENDOR demo `pwd`/demo" >> license.dat
echo FEATURE f1 demo 1.0 permanent 4 E4123D3F54B8 >> license.dat

popd >& /dev/null

#
#	See if they want to configure the daemon
#
echo ""
echo "If your FLEXlm DEMO keys have expired, you will need to re-run the"
echo "CONFIG_DAEMON script to enter the new DEMO vendor keys.  If this"
echo is the case, answer \"y\" to the next question.
echo Also, answer \"y\" when asked if you want to build the daemon.
echo ""
echo -n "Do you want to configure your daemon now [y/n]? "
if ($onsco == "TRUE") then
	set resp = `head -1 < /dev/tty`
else
	set resp = ($<)
endif
#
#	This tests for the first char in resp to be [Yy]
#
echo $resp | grep -i '^y' >& /dev/null
if ($status == 0) then
	./CONFIG_DAEMON demo
else
	echo "Building FLEXlm executables..."
	pushd $kit >& /dev/null
	make
	popd >& /dev/null
endif
#
# re-encrypt license.dat
#	
pushd $kit >& /dev/null
if ( -e license.dat ) then
	./lmcrypt license.dat
endif
#
#	Start lmgrd
#

echo -n "Do you want to start the license daemons now [y/n]? "
if ($onsco == "TRUE") then
	set resp = `head -1 < /dev/tty`
else
	set resp = ($<)
endif
echo $resp | grep -i '^y' >& /dev/null
if ($status == 0) then
	./lmgrd -c license.dat > LOG &
	echo ""
	echo "lmgrd started ... log file is $kit/LOG"
	echo "To re-run lmgrd, simply type:"
	echo "	% cd $kit"
	echo "	% lmgrd -c license.dat > LOG "
	echo ""
	echo "Now, you can run the lmclient program.  Your license file"
else
	echo ""
	echo "You will need to start the license daemons before you can"
	echo "run the lmclient program.  To start the license daemons"
	echo "enter the following commands:"
	echo ""
	echo "	% cd $kit"
	echo "	% lmgrd -c license.dat > LOG "
	echo ""
	echo "Once the license daemons are running, you can run the lmclient"
	echo "program to checkout some licenses.  Your license file"
endif

echo "has 4 licenses, so you should be able to run 4 copies before"
echo "the daemon will deny new requests."
echo ""
echo "Remember to cd to your kit directory, and before"
echo "you run lmclient, be sure to set the environment"
echo "variable LM_LICENSE_FILE to `pwd`/license.dat"
echo ""
echo "e.g.:"
echo "	% cd $kit"
echo "	% setenv LM_LICENSE_FILE `pwd`/license.dat"
echo ""
echo "To shut down the license daemons, type:"
echo "	% cd $kit"
echo "	% lmdown -c license.dat -q"
echo ""
